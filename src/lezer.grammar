@top Grammar { 
  Top | Rule | Skip | Precedence | Tokens | Comment
}

Top {
  "@top" Rule
}

Skip {
  "@skip" "{" MainSkip? "}" |
  "@skip" "{" MainSkip? "}" "{" ScopedSkip "}"
}

MainSkip {
  ruleExpr
}

ScopedSkip {
  Rule*
}

Precedence {
  "@precedence" "{" precedenceItem ("," precedenceItem)* "}"
}

precedenceItem {
  PrecedenceItem |
  PrecedenceItemCut |
  PrecedenceItemLeft |
  PrecedenceItemRight
}

PrecedenceItem {
  Identifier
}

PrecedenceItemCut {
  Identifier "@cut"
}

PrecedenceItemLeft {
  Identifier "@left" 
}

PrecedenceItemRight {
  Identifier "@right"
}

Tokens {
  "@tokens" "{" tokenRule+ "}"
}

tokenRule {
  Rule | TokenPrecedence | literalToken
}

literalToken {
  LiteralString NodeProp? | LiteralRegexp NodeProp?
}

TokenPrecedence {
  "@precedence" "{" tokenPrecedenceItem ("," tokenPrecedenceItem)* "}"
}

tokenPrecedenceItem {
  LiteralString | LiteralString | Identifier
}

Rule { 
  Identifier NodeProp? TemplateArguments? "{" ruleExpr "}"
}

RuleSimple {
  Identifier NodeProp? "{" ruleExpr "}"
}

ruleExpr {
  ChoiceExpr | term | RuleSimple
}

ChoiceExpr {
  term ( "|" term )+
}

term {
  ( element | repeat )+
}

Negate {
  "!" element
}

RepeatPlus {
  element "+"
}

RepeatAst {
  element "*"
}

RepeatOpt {
  element "?"
}

repeat {
  Negate | RepeatPlus | RepeatAst | RepeatOpt
}

element {
  Identifier | TemplateInstance | LiteralString | LiteralRegexp | "(" ruleExpr ")" | Any
}

TemplateInstance {
  Identifier TemplateInstanceArguments
}

TemplateArguments {
  "<" Identifier ("," Identifier)* ">"
}

TemplateInstanceArguments {
  "<" templateInstanceArguments ("," templateInstanceArguments)* ">"
}

templateInstanceArguments {
  term | ChoiceExpr
}

NodeProp {
  "[" PropName "=" PropValue "]"
}

PropName {
  identifier | at_keyword
}

PropValue {
  "{" identifier "}" | propValue
}

Identifier {
  identifier
}

LiteralString {
  dq_string | sq_string
}

LiteralRegexp {
  regexp
}

@tokens {
  propValue {
    $[a-zA-Z0-9]+
  }
  identifier {
    $[a-zA-Z] $[a-zA-Z0-9_.]*
  }
  at_keyword {
    "@" $[a-zA-Z_] $[a-zA-Z0-9_]*
  }
  regexp { "$[" reg_char+ "]" | "![" reg_char+ "]" }
  reg_char { $[\u{20}-\u{5c}\u{5e}-\u{10ffff}] | "\\" esc | "\\]" }
  dq_string { '"' dq_char* '"' }
  dq_char { $[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc }
  sq_string { "'" sq_char* "'" }
  sq_char { $[\u{20}-\u{26}\u{28}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc }
  esc { $["\\bfnrtu] }
  whitespace { $[ \n\r\t] }
  Comment { "//" ![\n]* }
  Any { "_" }
}

@skip { whitespace }